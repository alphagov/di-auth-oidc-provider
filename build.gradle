plugins {
    id "com.avast.gradle.docker-compose" version "0.14.3"
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'com.avast.gradle.docker-compose'

dockerCompose {
    buildBeforeUp = true
    forceRecreate = true
    startedServices = [
            'op-db',
            'flyway',
    ]
    def logDir = new File(project.buildDir, "logs")
    if (!logDir.exists()) {
        println("creating logs folder...")
        logDir.mkdir()
    }
    captureContainersOutput = false
    captureContainersOutputToFile = new File('logs', 'docker-compose-gradle.log')
    projectName = "di-auth-oidc-provider"
}

group 'uk.gov.di'

repositories {
    mavenCentral()
}

distributions {
    main {
        contents {
            from {
                [
                        'oidc-provider.yml'
                ]
            }

        }
    }
}

def dependencyVersions = [
        dropwizard:'2.0.21',
        nimbus_oauth2_sdk: '9.3.1',
        nimbus_jose_jwt: '9.8.1',
        junit_version: '5.6.0',
        mockito_version: '3.9.0',
        jdbi_version: '3.19.0',
        srp_version: '2.1.0'
]

dependencies {
    implementation "com.nimbusds:oauth2-oidc-sdk:${dependencyVersions.nimbus_oauth2_sdk}",
            "com.nimbusds:nimbus-jose-jwt:${dependencyVersions.nimbus_jose_jwt}",
            "io.dropwizard:dropwizard-core:${dependencyVersions.dropwizard}",
            "io.dropwizard:dropwizard-client:${dependencyVersions.dropwizard}",
            "io.dropwizard:dropwizard-views-mustache:${dependencyVersions.dropwizard}",
            "io.dropwizard:dropwizard-assets:${dependencyVersions.dropwizard}",
            "io.dropwizard:dropwizard-jdbi3:${dependencyVersions.dropwizard}",
            "org.jdbi:jdbi3-postgres:${dependencyVersions.jdbi_version}",
            "org.jdbi:jdbi3-jackson2:${dependencyVersions.jdbi_version}",
            "org.postgresql:postgresql:42.2.19",
            "software.amazon.awssdk:cognitoidentityprovider:2.16.43",
            "software.amazon.awssdk:cognitoidentity:2.16.43",
            "com.nimbusds:srp6a:${dependencyVersions.srp_version}"

    constraints {
        implementation 'com.fasterxml.jackson.core:jackson-core:2.12.3'
    }

    testImplementation "org.junit.jupiter:junit-jupiter-api:${dependencyVersions.junit_version}",
        "io.dropwizard:dropwizard-testing:${dependencyVersions.dropwizard}",
            "org.mockito:mockito-core:${dependencyVersions.mockito_version}",
            "org.mockito:mockito-junit-jupiter:${dependencyVersions.mockito_version}"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${dependencyVersions.junit_version}"
}

test {
    useJUnitPlatform()
    workingDir = rootDir
}

mainClassName = 'uk.gov.di.OidcProviderApplication'

run {
    args = ['server', 'oidc-provider.yml']
    debugOptions {
        enabled = true
        port = 8082
        server = true
        suspend = false
    }
}

task cfPush(type: Exec) {
    commandLine 'cf', 'push', 'di-auth-oidc-provider', '-p', 'build/distributions/di-auth-oidc-provider.zip'
}

cfPush.dependsOn build
rootProject.dockerCompose.isRequiredBy(run)